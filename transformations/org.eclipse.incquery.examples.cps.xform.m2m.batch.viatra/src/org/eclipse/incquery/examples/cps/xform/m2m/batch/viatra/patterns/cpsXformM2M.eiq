package org.eclipse.incquery.examples.cps.xform.m2m.batch.viatra.patterns

import "http://org.eclipse.incquery/model/cps"
import "http://org.eclipse.incquery/model/deployment"
import "http://org.eclipse.incquery/model/cps-traceability"

import java org.eclipse.incquery.examples.cps.xform.m2m.util.SignalUtil

pattern hostInstance(hostInstance) {
	HostInstance(hostInstance);
}

pattern cps2depTrace(cps2dep, trace, cpsElement, depElement) {
	CPSToDeployment.traces(cps2dep, trace);
	CPS2DeplyomentTrace.cpsElements(trace, cpsElement);
	CPS2DeplyomentTrace.deploymentElements(trace, depElement);
}

pattern applicationInstance(appType, appInstance) {
	HostInstance.applications(_, appInstance);
	ApplicationType.instances(appType, appInstance);
}

pattern appInstanceWithStateMachine(appType, appInstance, stateMachine) {
	find applicationInstance(appType, appInstance);
	ApplicationType.behavior(appType, stateMachine);
}

pattern state(appInstance, stateMachine, state) {
	find appInstanceWithStateMachine(_, appInstance, stateMachine);
	StateMachine.states(stateMachine, state);
}

pattern transition(appInstance, sourceState, transition, targetState) {
	find state(appInstance, _, sourceState);
	find state(appInstance, _, targetState);
	State.outgoingTransitions(sourceState, transition);
	Transition.targetState(transition, targetState);
}

pattern action(appInstance, transition, action, signalId) {
	find transition(appInstance, _, transition, _);
	Transition.action(transition, action);
	signalId == eval(SignalUtil.getSignalId(action));
}

pattern sendAction(appInstance, transition, action, signalId, appTypeId) {
	find action(appInstance, transition, action, signalId);
	check(SignalUtil.isSend(action));
	appTypeId == eval(SignalUtil.getAppId(action));
}

pattern waitAction(appInstance, transition, action, signalId) {
	find action(appInstance, transition, action, signalId);
	check(SignalUtil.isWait(action));
}

pattern actionPair(sourceBehaviorTransition, targetBehaviorTransition) {
	find sendAction(sendAppInstance, sourceTransition, _, signalId, appTypeId);
	find waitAction(waitAppInstance, targetTransition, _, signalId);
	ApplicationInstance.type.id(waitAppInstance, appTypeId);
	ApplicationInstance.allocatedTo(sendAppInstance, sendHostInstance);
	ApplicationInstance.allocatedTo(waitAppInstance, waitHostInstance);
	find reachableHosts(sendHostInstance, waitHostInstance);
	find cps2depTrace(_, _, sourceTransition, sourceBehaviorTransition);
	find cps2depTrace(_, _, targetTransition, targetBehaviorTransition);
	BehaviorTransition(sourceBehaviorTransition);
	BehaviorTransition(targetBehaviorTransition);
}

pattern reachableHosts(source, target) {
	HostInstance.communicateWith(source, target);
} or {
	HostInstance(source);
	HostInstance(target);
	source == target;
}